
PHDRS
{
  boot PT_LOAD ;
  mem PT_LOAD ;
}

SECTIONS
   {
	/* must be within 8192 first bytes of kernel image */
	. = 0x00100000;

	.multiboot : { *(.multiboot) } : boot

	.boot : { *(.boot) }
	__boot_start = LOADADDR(.boot);
	__boot_end = LOADADDR(.boot) + SIZEOF(.boot);

	.text : { *(.text) } : mem
	.bss : { *(.bss) }
	.data :
		{
			/* global variables and const data section */
			*(.data)
			*(.rodata*)

			/* data depending on cpu architecture (fonction pointer variables, ...) */
			*(.cpuarchdata)
		}

	__system_heap_start = .;

	/* Task local data section */
 	.contextdata 0x0 : AT( LOADADDR(.cpudata) + SIZEOF(.cpudata ) )
		{
			/* first word contains pointer to contextdata section */
			__context_data_base = . ;
			LONG(0) ;

			*(.contextdata)
		}

	__context_data_start = LOADADDR(.contextdata);
	__context_data_end = LOADADDR(.contextdata) + SIZEOF(.contextdata);

	/* CPU local data section */
	.cpudata  0x0 : AT( LOADADDR(.data) + SIZEOF(.data) )
		{
			/* first word contains pointer to cpudata section */
			__cpu_data_base = . ;
			LONG(0) ;

			/* second word contains pointer to contextdata section */
			__cpu_context_data_base = . ;
			LONG(0) ;

			*(.cpudata)
		}

	__cpu_data_start = LOADADDR(.cpudata);
	__cpu_data_end = LOADADDR(.cpudata) + SIZEOF(.cpudata);

   }

ENTRY(cpu_boot)

