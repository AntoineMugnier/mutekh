
PHDRS
{
  boot PT_LOAD AT(0x00100000);
  local PT_LOAD;
  mem PT_LOAD AT(0x0010000c);
}

SECTIONS
   {
	/* must be within 8192 first bytes of kernel image */
	. = 0x00100000;

	.multiboot : { *(.multiboot*) } : boot

	. = 0x0010000c;

	.boot : { *(.boot*) } : mem
	__boot_start = LOADADDR(.boot);
	__boot_end = LOADADDR(.boot) + SIZEOF(.boot);

	.text : { *(.text*) }
	.data :
		{
			/* global variables and const data section */
			*(.data*)
			*(.rodata*)
			global_driver_registry = .;
			*(.drivers)
			global_driver_registry_end = .;

			/* data depending on cpu architecture (fonction pointer variables, ...) */
			*(.cpuarchdata*)
		}

	.bss : { *(.bss*) }

	/* CPU local data section */
	.cpudata  0x0 : AT( LOADADDR(.bss) + SIZEOF(.bss) )
		{
			*(.cpudata*)
		} : local

	__cpu_data_start = LOADADDR(.cpudata);
	__cpu_data_end = LOADADDR(.cpudata) + SIZEOF(.cpudata);

	/* Task local data section */
 	.contextdata 0x0 : AT( __cpu_data_end )
		{
			*(.contextdata*)
		}

	__context_data_start = LOADADDR(.contextdata);
	__context_data_end = LOADADDR(.contextdata) + SIZEOF(.contextdata);

	__system_heap_start = __context_data_end;

   }

ENTRY(cpu_boot)

