
PHDRS
{
  signature PT_NOTE ;
  boot PT_LOAD ;
  mem PT_LOAD ;
}

SECTIONS
   {
	/* must be within 8192 first bytes of kernel image */
	.multiboot (INFO) : { *(.multiboot) } : signature

	. = 0x00011000;

	.boot : { *(.boot) } : boot

	. = 0x00100000;
	.text : { *(.text) } : mem
	.bss : { *(.bss) }
	.data :
		{
			/* global variables and const data section */
			*(.data)
			*(.rodata*)

			/* data depending on cpu architecture (fonction pointer variables, ...) */
			*(.cpuarchdata)
		}

	/* Task local data section */
 	.contextdata 0x0 : AT( LOADADDR(.cpudata) + SIZEOF(.cpudata ) )
		{
			/* first word contains pointer to contextdata section */
			__context_data_base = . ;
			LONG(0) ;

			*(.contextdata)
		}

	__context_data_start = LOADADDR(.contextdata);
	__context_data_end = LOADADDR(.contextdata) + SIZEOF(.contextdata);

	/* CPU local data section */
	.cpudata  0x0 : AT( LOADADDR(.data) + SIZEOF(.data) )
		{
			/* first word contains pointer to cpudata section */
			__cpu_data_base = . ;
			LONG(0) ;

			/* second word contains pointer to contextdata section */
			__cpu_context_data_base = . ;
			LONG(0) ;

			*(.cpudata)
		}

	__cpu_data_start = LOADADDR(.cpudata);
	__cpu_data_end = LOADADDR(.cpudata) + SIZEOF(.cpudata);

   }

ENTRY(cpu_boot)

