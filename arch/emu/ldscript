
PHDRS
{
  local PT_LOAD ;
  mem PT_LOAD ;
}

SECTIONS
   {
	.text 0x80100000 : AT( 0x80100000 ) { *(.text*) } : mem

	.data :
		{
			/* global variables and const data section */
			*(.data*)
			*(.rodata*)

			/* data depending on cpu architecture (fonction pointer variables, ...) */
			*(.cpuarchdata*)
		}

	.bss : { *(.bss*) }

	/* CPU local data section */
	.cpudata 0x0 : AT( LOADADDR(.bss) + SIZEOF(.bss) )
		{
			*(.cpudata*)
		}

	__cpu_data_start = LOADADDR(.cpudata);
	__cpu_data_end = LOADADDR(.cpudata) + SIZEOF(.cpudata);

	/* Task local data section */
 	.contextdata 0x0 : AT( __cpu_data_end )
		{
			*(.contextdata*)
		} : local

	__context_data_start = LOADADDR(.contextdata);
	__context_data_end = LOADADDR(.contextdata) + SIZEOF(.contextdata);

	__system_heap_start = __context_data_end;

   }

ENTRY(arch_init)

