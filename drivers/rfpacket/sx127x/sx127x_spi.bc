/*
   This file is part of MutekH.

    MutekH is free software; you can redistribute it and/or modify it
    under the terms of the GNU Lesser General Public License as
    published by the Free Software Foundation; version 2.1 of the
    License.

    MutekH is distributed in the hope that it will be useful, but
    WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
    Lesser General Public License for more details.

    You should have received a copy of the GNU Lesser General Public
    License along with MutekH; if not, write to the Free Software
    Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
    02110-1301 USA.

    Copyright (c) 2018 Sebastien Cerdan <sebcerdan@gmail.com>

*/

#include <device/class/spi.h>
#include "sx127x_spi.h"

        .custom spi
        .name sx127x
        .const %R_CTX_PV
        .global %STATUS

.func sx127x_entry_reset
        .export sx127x_entry_reset
        .clobber %5 tmp, %6 data, %7 ver
    /* Get start-up delay */
        ld32e                   %data,            %R_CTX_PV,       _offsetof(sx127x_private_s, delay_1ms, 4)
        shi32l                  %data,            2
    /* Reset device */
        cst8                    %tmp,             0
        spi_gpioset             SX127X_IO_RST,    %tmp
        spi_yield_delay         %data,
        cst8                    %tmp,             1
        spi_gpioset             SX127X_IO_RST,    %tmp
        spi_yield_delay         %data
    /* Check Revision */
        cst8                    %tmp,             SX1276_REG_VERSION
        pack16le                %tmp,             1,              2
        spi_swp                 %tmp,             %tmp,           2,      CS_PULSE
        unpack8                 %tmp,             2
        cst32                   %ver,             SX127X_VERSION_VALUE,   0
        neq                     %ver,             %data
          abort
    /* Rssi measure configuration */
        cst8                    %tmp,             SX1276_REG_RSSICONFIG | 0x80
        cst8                    %data,            (SX127X_RSSI_OFFSET | SX1276_RSSICONFIG_SMOOTHING_64)
        pack8                   %tmp,             2
        spi_wr                  %tmp,             2,     CS_PULSE
        jmp32                   sx127x_entry_irq
.endfunc

.func sx127x_config
        .input %0 buf, %11 link
        .clobber %0, %1 cmd, %6 len, %7, %8, %9, %10, %12

        /* Ensure Stand-by mode */
        cst8                    %sx127x_mode_set:mode,  SX1276_OPMODE_STANDBY
        call16                  %sx127x_mode_set:link,  sx127x_mode_set
        call8                   %config_freq:link,      config_freq

  config_loop:
        ld8i                    %len,                %buf
        eq0                     %len
          jmp8 config_done
        ld8i                    %cmd,                %buf
        pack8                   %cmd,         1
        spi_wr                  %cmd,         1,                CS_START
        spi_wrm                 %buf,                %len,	CS_END
        add                     %buf,                %len
        jmp8                    config_loop
  config_done:
        ret                     %link
.endfunc

.func sx127x_entry_config
        .export sx127x_entry_config
        .input %0 buf
        .clobber %0, %1 cmd, %6 len, %7, %8, %9, %10, %11, %12
        call8                   %sx127x_config:link,            sx127x_config
        end
.endfunc

.func sx127x_entry_irq
        .export sx127x_entry_irq
        .clobber %0, %5, %6, %7, %8, %9, %10, %12
        call8                   %sx127x_check_irq:link,          sx127x_check_irq
        end
.endfunc

.func sx127x_check_irq
        .input %12 link
        .clobber %0 buffer, %5 tmp status, %6 irq1 len, %7 irq2, %8, %9, %10, %12
        /* Update icount field */
        //cst32                   %STATUS,         0,   0
        //ld8e                    %STATUS,         %R_CTX_PV,      _offsetof(sx127x_private_s, icount, 1)
        spi_gpioget             SX127X_IO_DIO0,     %tmp
        tst32s                  %tmp,            0
          jmp8                  __sx127x_process_irq
        ret                     %link

__sx127x_process_irq:
        /* Read irq status */
        cst8                    %tmp,            SX1276_REG_IRQFLAGS1
        pack32le                %tmp,            1,      3
        spi_swp                 %tmp,            %tmp,   3,      CS_PULSE
        unpack8                 %tmp,            3
        /* Set the transceiver in standby mode */
        cst8                    %sx127x_mode_set:mode,  SX1276_OPMODE_STANDBY
        call8                   %sx127x_mode_set:link,  sx127x_mode_set
        mov                     %tmp,            %irq2
        shi32l                  %tmp,            8
        or32                    %STATUS,         %tmp
        tst32s                  %irq2,           bitpos(SX1276_IRQFLAGS2_PACKETSENT)
          jmp8                  __irq_tx_flag
        tst32s                  %irq2,           bitpos(SX1276_IRQFLAGS2_PAYLOADREADY)
          jmp8                  __irq_rx_flag
        end
__irq_rx_flag:
        /* Retrieve first word from fifo which contains payload length */
        cst8                    %tmp,            SX1276_REG_FIFO
        pack16le                %tmp,            1,      2
        spi_swp                 %tmp,            %tmp,   2,      CS_PULSE
        unpack8                 %tmp,            2
        st16e                   %len,            %R_CTX_PV,   _offsetof(sx127x_private_s, gctx.size, 2)
       /* Try to allocate buffer */
        gaddr                   %tmp,            sx127x_alloc
        mov                     %0,              %R_CTX_PV
        ccall                   %tmp
        /* Rx allocation error */
        eq0                     %buffer
          jmp8                  __rx_alloc_error
        /* Read Fifo */
        ld16e                   %len,            %R_CTX_PV,      _offsetof(sx127x_private_s, gctx.size, 2)
        cst8                    %tmp,            SX1276_REG_FIFO
        pack8                   %tmp,            1
        spi_wr                  %tmp,            1,             CS_START
        spi_rdm                 %buffer,         %len,          CS_END
        // Set bc status
        cst8                    %status,         SX127X_BC_STATUS_RX_DONE
        st8e                    %status,         %R_CTX_PV,   _offsetof(sx127x_private_s, bc_status, 1)
        end
__irq_tx_flag:
        // Set bc status
        cst8                    %status,         SX127X_BC_STATUS_TX_DONE
        st8e                    %status,         %R_CTX_PV,   _offsetof(sx127x_private_s, bc_status, 1)
        end
__rx_alloc_error:
        jmp8                    sx127x_error
.endfunc

.func sx127x_error
        .clobber %0 status, %8, %9, %10, %12
        // Set bc status
        cst8                    %status,         SX127X_BC_STATUS_OTHER_ERR
        st8e                    %status,         %R_CTX_PV,   _offsetof(sx127x_private_s, bc_status, 1)
        jmp8                    sx127x_entry_standby
.endfunc

.func sx127x_entry_standby
        .export sx127x_entry_standby
        .clobber %0 tmp, %8, %9, %10, %12
        cst8                    %sx127x_mode_set:mode,  SX1276_OPMODE_STANDBY
        call8                   %sx127x_mode_set:link,  sx127x_mode_set
        /* Clean irq status */
        cst32                   %tmp,            STATUS_INFO_MSK, 0
        and32                   %STATUS,         %tmp
        // ld8e                    %tmp,            %R_CTX_PV,      _offsetof(sx127x_private_s, icount, 1)
        // or32                    %STATUS,         %tmp
        end
.endfunc

.func config_freq
        .input %12 link
        .clobber %6 tmp, %7 data, %8 len
    /* Update frequency */
        mov                     %data,           %R_CTX_PV
        cst16                   %tmp,            _offsetof(sx127x_private_s, cfg_regs.freq),  0
        add                     %data,           %tmp
        cst8                    %tmp,            SX1276_REG_FRFMSB | 0x80
        pack8                   %tmp,            1
        spi_wr                  %tmp,            1,              CS_START
        cst8                    %len,            3
        spi_wrm                 %data,           %len,           CS_END
        ret                     %link
.endfunc

.func sx127x_entry_cancel
        .export sx127x_entry_cancel
        .clobber %0, %5, %6, %7, %8, %9, %10, %11, %12
        /* Test if packet is being received */
        call8                   %test_rx_on_going:link, test_rx_on_going
        jmp8                    sx127x_entry_standby
.endfunc


.func sx127x_mode_set
        .input %8 mode, %12 link
        .clobber %9 tmp, %10 cfg irq1
        cst8                    %tmp,            SX1276_REG_OPMODE | 0x80
        ld8e                    %cfg,            %R_CTX_PV,   _offsetof(sx127x_private_s, cfg_regs.mode, 1)
        or32                    %cfg,            %mode
        pack8                   %tmp,            2
        spi_wr                  %tmp,            2,     CS_PULSE
        ret                     %link
.endfunc

.func sx127x_entry_rx
        .export sx127x_entry_rx
        .input %2 cmd
        .clobber %0 deadline timeout status, %5, %6 tmp, %7 data, %8, %9, %10, %11, %12
    /* Set sync word size */
        mov                     %data,        %cmd
        cst8                    %tmp,         (SX1276_REG_SYNCCONFIG | 0x80)
        pack8	                %tmp,         2
        spi_wr                  %tmp,         2,      CS_PULSE
    /* Test if frequency must be changed */
        tst32s                  %cmd,         bitpos(CMD_FREQ_CHANGE)
          call8                 %config_freq:link,  config_freq
    /* Wait for deadline if any */
        mov                     %deadline,     %R_CTX_PV
        add8                    %deadline,     _offsetof(sx127x_private_s, gctx.deadline, 8)
        spi_yield_deadline      %deadline
    /* Start RX */
__retry_rx:
        cst8                    %sx127x_mode_set:mode,  SX1276_OPMODE_RECEIVER
        call8                   %sx127x_mode_set:link,  sx127x_mode_set
    /* Stay in RX until lifetime is up */
        mov                     %timeout,      %R_CTX_PV
        add8                    %timeout,      _offsetof(sx127x_private_s, gctx.timeout, 8)
        spi_yieldc_deadline     %timeout
          jmp8                  __rx_timeout
    /* An interrupt has been raised */
        call8                   %sx127x_check_irq:link,  sx127x_check_irq
    /* Not a packet IRQ, check preamble */
        call8                   %test_rx_on_going:link,  test_rx_on_going
        jmp8                    __retry_rx
__rx_timeout:
        // Set bc status
        cst8                    %status,         SX127X_BC_STATUS_RX_TIMEOUT
        st8e                    %status,         %R_CTX_PV,   _offsetof(sx127x_private_s, bc_status, 1)
        /* Test if packet is being received */
        call8                   %test_rx_on_going:link, test_rx_on_going
        jmp8                    sx127x_entry_standby

.endfunc

.func sx127x_entry_rx_cont
        .export sx127x_entry_rx_cont
        .input %0 cmd
        .clobber %0 chan, %1 timeout, %5, %6 tmp, %7 data, %8, %9, %10, %11, %12
    /* Set sync word size */
        mov                     %data,        %cmd
        cst8                    %tmp,         (SX1276_REG_SYNCCONFIG | 0x80)
        pack8	                %tmp,         2
        spi_wr                  %tmp,         2,      CS_PULSE
    /* Test if frequency must be changed */
        tst32s                  %cmd,         bitpos(CMD_FREQ_CHANGE)
          call8                 %config_freq:link,  config_freq
    /* Set channel scanning delay */
        ld32e                   %timeout,     %R_CTX_PV,   _offsetof(sx127x_private_s, tpbrx, 4)
    /* Start RX */
        cst8                    %sx127x_mode_set:mode,  SX1276_OPMODE_RECEIVER
        call8                   %sx127x_mode_set:link,  sx127x_mode_set
    /* Test if channel scanning must be performed */
        tst32s                  %cmd,         bitpos(CMD_RX_SCANNING)
          jmp8                  __rx_hopping_loop
    /* Check preamble detecion before leaving */
        call8                   %test_rx_on_going:link, test_rx_on_going
        end
__rx_hopping_loop:
   /* Wait channel scanning delay */
        spi_delay               %timeout
   /* Wait scanning delay */
        spi_yieldc
          jmp8                  __rx_check_status
        call8                   %sx127x_check_irq:link,  sx127x_check_irq
__rx_check_status:
   /* Test if packet is being received */
        call8                   %test_rx_on_going:link,  test_rx_on_going
   /* Check if a cancel is pending before scanning next channel */
        ld8e                   %tmp,            %R_CTX_PV,   _offsetof(sx127x_private_s, flags, 1)
        tst32c                 %tmp,            bitpos(SX127X_FLAGS_RX_CONTINOUS)
          jmp8                    sx127x_entry_cancel
   /* Stop receiver */
        cst8                    %sx127x_mode_set:mode,  SX1276_OPMODE_STANDBY
        call8                   %sx127x_mode_set:link,  sx127x_mode_set
   /* Compute next frequency */
        gaddr                   %tmp,               sx127x_next_hopping_freq
        ccall                   %tmp
        call8                   %config_freq:link,  config_freq
   /* Restart Rx on next channel */
        cst8                    %sx127x_mode_set:mode,  SX1276_OPMODE_RECEIVER
        call8                   %sx127x_mode_set:link,  sx127x_mode_set
        jmp8                    __rx_hopping_loop
.endfunc

.func test_rx_on_going
        .input %11 link
        .clobber %0, %5, %6 tmp, %7, %8, %9, %10, %12
        spi_gpioget             SX127X_IO_DIO4,  %tmp
   /* Test if a valid preamble has been received */
        tst32c                  %tmp,            0
          jmp8                  __no_rx_ongoing
   /* Receive is on-going */
        ld32e                   %tmp,         %R_CTX_PV,   _offsetof(sx127x_private_s, tbrs, 4)
        spi_yieldc_delay        %tmp
          jmp8                  __no_rx_ongoing
   /* A payload ready irq has been raised */
        call16                  %sx127x_check_irq:link,  sx127x_check_irq
__no_rx_ongoing:
        ret                     %link
.endfunc

.func process_tx_cmd
        .input %2 cmd
        .input %11 link
        .clobber %6 tmp, %7 data, %8, %12
        /* Power config */
        mov                     %data,        %cmd
        shi32r                  %data,        8
        cst8                    %tmp,         SX1276_REG_PACONFIG | 0x80
        pack8                   %tmp,         2
        spi_wr                  %tmp,         2,      CS_PULSE
        /* Change frequency is needed */
        tst32s                  %cmd,         bitpos(CMD_FREQ_CHANGE)
          call8                 %config_freq:link,  config_freq
        /* Set sync word size */
        mov                     %data,        %cmd
        cst8                    %tmp,         (SX1276_REG_SYNCCONFIG | 0x80)
        pack8	                %tmp,         2
        spi_wr                  %tmp,         2,      CS_PULSE
        ret                     %link
.endfunc

.func sx127x_read_rssi
        .input %12 link
        .clobber %6 tmp
        .output %7 data
        /* Read RSSI */
        cst8                    %tmp,            SX1276_REG_RSSIVALUE
        pack16le                %tmp,            1,      2
        spi_swp                 %tmp,            %tmp,   2,      CS_PULSE
        unpack8                 %tmp,            2
        ret                     %link
.endfunc

.func sx127x_entry_retry_tx_fair
        .input %2 cmd
        .export sx127x_entry_retry_tx_fair
        .clobber %3, %4, %5, %6, %7, %8, %9, %10, %12
        /* Jump to main function */
        jmp8                    sx127x_main_tx_fair
.endfunc

.func sx127x_entry_tx_fair
        .export sx127x_entry_tx_fair
        .input %2 cmd
        .clobber %3 deadline, %4, %5, %6, %7, %8, %9, %10, %11, %12
        /* Init TX */
        call8                   %process_tx_cmd:link,    process_tx_cmd
        /* Wait deadline before TX */
        mov                     %deadline,    %R_CTX_PV
        add8                    %deadline,    _offsetof(sx127x_private_s, gctx.deadline, 8)
        spi_yield_deadline      %deadline
        /* Jump to main function */
        jmp8                    sx127x_main_tx_fair
.endfunc

.func sx127x_main_tx_fair
        .input %2 cmd
        .clobber %3 mean status, %4 timeout, %5 period, %6 rssith, %7 data, %8, %9, %10, %12
        /* Set Rssi period */
        ld32e                   %period,      %R_CTX_PV,   _offsetof(sx127x_private_s, delay_1ms, 4)
        shi32l                  %period,      1
        /* Test if timeout date is reached */
        mov                     %timeout,     %R_CTX_PV
        add8                    %timeout,     _offsetof(sx127x_private_s, gctx.timeout, 8)
        spi_deadline            %timeout
        spi_elapsed
          jmp8                  __tx_fail
        /* Start RX */
        cst8                    %sx127x_mode_set:mode,  SX1276_OPMODE_RECEIVER
        call8                   %sx127x_mode_set:link,  sx127x_mode_set
__retry_tx_fair:
        spi_yield_delay         %period
        call8                   %sx127x_read_rssi:link,  sx127x_read_rssi
        mov                     %mean,        %data
        /* Wait before second measurement */
        spi_yield_delay         %period
        call8                   %sx127x_read_rssi:link,  sx127x_read_rssi
        add                     %mean,        %data
        ld8e                    %rssith,      %R_CTX_PV,   _offsetof(sx127x_private_s, cfg_regs.rssi_th, 2)
        lt                      %rssith,      %mean
        /* RSSI mean is less than RSSI threshold */
          jmp8                  sx127x_start_tx
        /* Test if timeout date is reached */
        spi_deadline            %timeout
        spi_elapsed
          jmp8                  __tx_fail
        jmp8                    __retry_tx_fair
__tx_fail:
        // Set bc status
        cst8                    %status,         SX127X_BC_STATUS_TX_TIMEOUT
        st8e                    %status,         %R_CTX_PV,   _offsetof(sx127x_private_s, bc_status, 1)
        jmp16                   sx127x_entry_standby
.endfunc

.func sx127x_entry_tx
        .export sx127x_entry_tx
        .input %2 cmd
        .clobber %0, %1, %3 deadline, %5, %6 tmp, %7 data, %8, %9, %10, %11, %12
        call8                   %process_tx_cmd:link,    process_tx_cmd
        /* Wait deadline before TX */
        mov                     %deadline,    %R_CTX_PV
        add8                    %deadline,    _offsetof(sx127x_private_s, gctx.deadline, 8)
        spi_yield_deadline      %deadline
        jmp8                    sx127x_start_tx
.endfunc

.func sx127x_start_tx
        .input %2 cmd
        .clobber %0 len, %1 buffer, %5, %6 tmp, %7 data, %8, %9, %10, %12
        /* Start TX */
        cst8                    %sx127x_mode_set:mode,  SX1276_OPMODE_TRANSMITTER
        call16                   %sx127x_mode_set:link,  sx127x_mode_set
        /* Write data values */
        ld16e                   %len,         %R_CTX_PV,  _offsetof(sx127x_private_s, gctx.size, 2)
        ld32e                   %buffer,      %R_CTX_PV,  _offsetof(sx127x_private_s, gctx.buffer, 4)
        /* Write Fifo */
        cst8                    %tmp,         SX1276_REG_FIFO | 0x80
        tst32c                  %cmd,         bitpos(CMD_IO_MODE)
          jmp8                  __send_len
	/* Do not send length in IO mode */
        pack8                   %tmp,         1
        spi_wr                  %tmp,         1,             CS_START
        jmp8                    __send_data
  __send_len:
        mov                     %data,        %len
        pack8                   %tmp,         2
        spi_wr                  %tmp,         2,             CS_START
  __send_data:
        spi_wrm                 %buffer,      %len,          CS_END
        /* Transmit is on-going */
        ld32e                   %tmp,         %R_CTX_PV,   _offsetof(sx127x_private_s, tbrs, 4)
        spi_yieldc_delay        %tmp
          jmp16                 sx127x_error
        call16                  %sx127x_check_irq:link,    sx127x_check_irq
        jmp16                   sx127x_error
.endfunc

#ifdef CONFIG_DRIVER_RFPACKET_SX127X_RAW_MODE
.func sx127x_entry_rx_raw
        .export sx127x_entry_rx_raw
        .input %0 cmd
        .clobber %6 tmp, %7 data, %8, %9, %10, %12
    /* Change frequency is needed */
        tst32s                  %cmd,               bitpos(CMD_FREQ_CHANGE)
          call16                %config_freq:link,  config_freq
    /* Start RX */
        cst8                    %sx127x_mode_set:mode,  SX1276_OPMODE_RECEIVER
        call16                  %sx127x_mode_set:link,  sx127x_mode_set
        end
.endfunc

.func sx127x_entry_tx_raw
        .export sx127x_entry_tx_raw
        .input %0 cmd
        .clobber %6 tmp, %7 data, %8, %9, %10, %12
    /* Power config */
        mov                     %data,        %cmd
        shi32r                  %data,        8
        cst8                    %tmp,         SX1276_REG_PACONFIG | 0x80
        pack8                   %tmp,         2
        spi_wr                  %tmp,         2,      CS_PULSE
    /* Change frequency is needed */
        tst32s                  %cmd,               bitpos(CMD_FREQ_CHANGE)
          call16                %config_freq:link,  config_freq
    /* Start TX */
        cst8                    %sx127x_mode_set:mode,    SX1276_OPMODE_TRANSMITTER
        call16                  %sx127x_mode_set:link,    sx127x_mode_set
        end
.endfunc

#endif

#ifdef SX127X_PRINT_REGS
.func sx127x_entry_dump_cfg
        .clobber %6 tmp, %7 data, %8, %9, %10, %12
        cst8                    %tmp,         SX1276_REG_OPMODE
        pack8                   %tmp,         1
        spi_wr                  %tmp,         1,                CS_START
        ld32e                   %tmp,         %R_CTX_PV,        _offsetof(sx127x_private_s, dump, 4)
        cst8                    %data,        112
        spi_rdm                 %tmp,         %data,          CS_END
        end
.endfunc
#endif